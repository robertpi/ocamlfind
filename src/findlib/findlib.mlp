(* $Id: findlib.mlp,v 1.11 2002/09/22 20:12:32 gerd Exp $
 * ----------------------------------------------------------------------
 *
 *)

let conf_default_location = ref "";;
let conf_meta_directory = ref "";;
let conf_search_path = ref [];;
let conf_command = ref [];;
let conf_stdlib = ref "";;
let conf_ldconf = ref "";;

let ocamlc_default = "ocamlc";;
let ocamlopt_default = "ocamlopt";;
let ocamlcp_default = "ocamlcp";;
let ocamlmktop_default = "ocamlmktop";;
let ocamldep_default = "ocamldep";;
let ocamlbrowser_default = "ocamlbrowser";;


module M = @METACACHE@;;
  (* M is either Fl_metacache or Fl_metacache_mt *)


let init_manually 
      ?(ocamlc_command = ocamlc_default)
      ?(ocamlopt_command = ocamlopt_default)
      ?(ocamlcp_command = ocamlcp_default)
      ?(ocamlmktop_command = ocamlmktop_default)
      ?(ocamldep_command = ocamldep_default)
      ?(ocamlbrowser_command = ocamlbrowser_default)
      ?(stdlib = Findlib_config.ocaml_stdlib)
      ?(ldconf = Findlib_config.ocaml_ldconf)
      ~install_dir
      ~meta_dir
      ~search_path =
  conf_command := [ `ocamlc,     ocamlc_command;
		    `ocamlopt,   ocamlopt_command;
		    `ocamlcp,    ocamlcp_command;
		    `ocamlmktop, ocamlmktop_command;
		    `ocamldep,   ocamldep_command;
		    `ocamlbrowser, ocamlbrowser_command;
		  ];
  conf_search_path := search_path;
  conf_default_location := install_dir;
  conf_meta_directory := meta_dir;
  conf_stdlib := stdlib;
  conf_ldconf := ldconf;
  M.init_cache !conf_search_path stdlib
;;


let command_names cmd_spec =
  try
    let cmd_list = Fl_split.in_words cmd_spec in
    List.map
      (fun cmd_setting ->
	 try
	   (* cmd_setting: formal_name=actual_name *)
	   let l = String.length cmd_setting in
	   let n = String.index cmd_setting '=' in
	   let cmd_formal_name = String.sub cmd_setting 0 n in
	   let cmd_actual_name = String.sub cmd_setting (n+1) (l-n-1) in
	   cmd_formal_name, cmd_actual_name
	 with
	     Not_found ->
	       prerr_endline ("Warning: Please check the environment variable OCAMLFIND_COMMANDS");
	       "", ""
      )
      cmd_list
  with
      Not_found ->
	[]
;;

let init
      ?env_ocamlpath ?env_ocamlfind_destdir ?env_ocamlfind_metadir
      ?env_ocamlfind_commands ?env_camllib ?env_ldconf
      ?config () =
  
  let config_file =
    match config with
	Some f -> f
      | None ->
	  let p =
	    ( try Sys.getenv "OCAMLFIND_CONF" with Not_found -> "") in
	  if p = "" then Findlib_config.config_file else p
  in

  let sys_ocamlc, sys_ocamlopt, sys_ocamlcp, sys_ocamlmktop, sys_ocamldep,
      sys_ocamlbrowser,
      sys_search_path, sys_destdir, sys_metadir, sys_stdlib, sys_ldconf = begin
    if Sys.file_exists config_file then begin
      let ch = open_in config_file in
      try
	let vars = Fl_metascanner.parse ch in
	let lookup name default =
	  try Fl_metascanner.lookup name [] vars
	  with Not_found -> default
	in
	let tuple =
	  ( (lookup "ocamlc" ocamlc_default),
	    (lookup "ocamlopt" ocamlopt_default),
	    (lookup "ocamlcp" ocamlcp_default),
	    (lookup "ocamlmktop" ocamlmktop_default),
	    (lookup "ocamldep" ocamldep_default),
	    (lookup "ocamlbrowser" ocamlbrowser_default),
	    Fl_split.path (lookup "path" ""),
	    (lookup "destdir" ""),
	    (lookup "metadir" "none"),
	    (lookup "stdlib" Findlib_config.ocaml_stdlib),
	    (lookup "ldconf" Findlib_config.ocaml_ldconf)
	  )
	in
	close_in ch;
	tuple
      with
	  exc -> 
	    close_in ch; raise exc
    end
    else
      ( ocamlc_default, ocamlopt_default, ocamlcp_default, ocamlmktop_default,
	ocamldep_default, ocamlbrowser_default,
	[],
	"",
        "none",
	Findlib_config.ocaml_stdlib,
	Findlib_config.ocaml_ldconf
      )
  end
  in

  let env_commands = 
    match env_ocamlfind_commands with
	Some x -> command_names x
      | None   -> command_names (try Sys.getenv "OCAMLFIND_COMMANDS"
				 with Not_found -> "")
  in
  let env_destdir = 
    match env_ocamlfind_destdir with
	Some x -> x
      | None   ->
	  try Sys.getenv "OCAMLFIND_DESTDIR" with Not_found -> "" 
  in
  let env_metadir = 
    match env_ocamlfind_metadir with
	Some x -> x
      | None   ->
	  try Sys.getenv "OCAMLFIND_METADIR" with Not_found -> "" 
  in
  let env_search_path = 
    Fl_split.path
      (match env_ocamlpath with
	   Some x -> x
	 | None -> 
	     try Sys.getenv "OCAMLPATH" with Not_found -> "" 
      )
  in
  let env_stdlib =
    match env_camllib with
	Some x -> x
      | None ->
	  ( try Sys.getenv "OCAMLLIB"
	    with 
		Not_found ->
		  (try Sys.getenv "CAMLLIB" with Not_found -> "" )
	  )
  in
  let env_ldconf =
    match env_ldconf with
	Some x -> x
      | None ->
	  try Sys.getenv "OCAMLFIND_LDCONF" with Not_found -> ""
  in

  let ocamlc, ocamlopt, ocamlcp, ocamlmktop, ocamldep, ocamlbrowser,
      search_path, destdir, metadir, stdlib, ldconf =
    (try List.assoc "ocamlc"     env_commands with Not_found -> sys_ocamlc),
    (try List.assoc "ocamlopt"   env_commands with Not_found -> sys_ocamlopt),
    (try List.assoc "ocamlcp"    env_commands with Not_found -> sys_ocamlcp),
    (try List.assoc "ocamlmktop" env_commands with Not_found -> sys_ocamlmktop),
    (try List.assoc "ocamldep"   env_commands with Not_found -> sys_ocamldep),
    (try List.assoc "ocamlbrowser" env_commands with Not_found -> sys_ocamlbrowser),
    (env_search_path @ sys_search_path),
    (if env_destdir = "" then sys_destdir else env_destdir),
    (if env_metadir = "" then sys_metadir else env_metadir),
    (if env_stdlib  = "" then sys_stdlib  else env_stdlib),
    (if env_ldconf  = "" then sys_ldconf  else env_ldconf)
  in

  init_manually
    ~ocamlc_command: ocamlc
    ~ocamlopt_command: ocamlopt
    ~ocamlcp_command: ocamlcp
    ~ocamlmktop_command: ocamlmktop
    ~ocamldep_command: ocamldep
    ~ocamlbrowser_command: ocamlbrowser
    ~stdlib: stdlib
    ~ldconf: ldconf
    ~install_dir: destdir
    ~meta_dir: metadir
    ~search_path: search_path
;;


let default_location() = !conf_default_location;;
let meta_directory() =
  if !conf_meta_directory = "none" then "" else !conf_meta_directory;;
let search_path() = !conf_search_path;;
let command which =
  try 
    List.assoc which !conf_command
  with
      Not_found -> assert false
;;
let ocaml_stdlib() = !conf_stdlib;;
let ocaml_ldconf() = !conf_ldconf;;


let package_directory pkg =
  (M.query pkg).Fl_metacache.package_dir
;;


let package_property predlist pkg propname =
  let l = M.query pkg in
  Fl_metascanner.lookup propname predlist l.Fl_metacache.meta_file
;;


let package_ancestors predlist pkg =
  M.requires predlist pkg
;;


let package_deep_ancestors predlist pkglist =
  M.requires_deeply predlist pkglist
;;


init();


(* ======================================================================
 * History:
 *
 * $Log: findlib.mlp,v $
 * Revision 1.11  2002/09/22 20:12:32  gerd
 * 	Renamed modules (prefix fl_)
 *
 * Revision 1.10  2002/04/26 15:45:22  gerd
 * 	New: ocamlfind browser
 *
 * Revision 1.9  2002/04/26 14:51:54  gerd
 * 	New option -ldconf
 *
 * Revision 1.8  2001/12/15 17:59:35  gerd
 * 	OCAMLLIB overrides CAMLLIB (O'Caml 3.04)
 *
 * Revision 1.7  2001/09/04 16:11:27  gerd
 * 	Added command ocamldep.
 *
 * Revision 1.6  2001/07/24 19:57:17  gerd
 * 	Supports options for stdlib
 *
 * Revision 1.5  2001/02/24 20:19:41  gerd
 * 	Support for configuration file. New functions init, init_manually.
 *
 * Revision 1.4  1999/06/24 20:17:49  gerd
 * 	Further modifications (dont know which...)
 *
 * Revision 1.3  1999/06/20 22:23:17  gerd
 * 	Works now with the core libraries.
 *
 * Revision 1.2  1999/06/20 19:26:23  gerd
 * 	Major change: Added support for META files. In META files, knowlege
 * about compilation options, and dependencies on other packages can be stored.
 * The "ocamlfind query" subcommand has been extended in order to have a
 * direct interface for that. "ocamlfind ocamlc/ocamlopt/ocamlmktop/ocamlcp"
 * subcommands have been added to simplify the invocation of the compiler.
 *
 * Revision 1.1  1999/03/26 00:02:47  gerd
 * 	Initial release.
 *
 *
 *)
